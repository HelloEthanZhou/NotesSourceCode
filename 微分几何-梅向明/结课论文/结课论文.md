[TOC]

# Mathematica 在微分几何研究中的交互式应用

# 摘要

本文探讨了 Mathematica 在微分几何研究中的交互式应用。首先介绍了曲线的 Frenet 系统和曲面的基本形式的定义，并使用 Mathematica 进行相应的计算。接着，通过交互式绘图，展示了经典曲线（如圆柱螺线）和曲面（如马鞍面）的几何特征，包括曲线的切线、基本三棱形以及曲面的法线和切平面。通过这些实例，本文展示了 Mathematica 在进行复杂代数运算和几何图形可视化方面的强大功能，及其在帮助理解和研究微分几何中的应用价值。

# 关键词

Mathematica，微分几何，交互式绘图，可视化

# 一、引言

微分几何是研究曲线和曲面几何特征的重要数学领域，涉及多种复杂的数学对象和计算。随着计算机技术的发展，现代数学软件如 Mathematica 在微分几何的研究中发挥了重要作用。Mathematica 强大的符号计算和可视化功能，使得研究者能够更高效地进行代数运算和几何图形的交互式展示，从而深入理解曲线和曲面的几何性质。

本文旨在展示 Mathematica 在微分几何研究中的应用，特别是其在曲线的 Frenet 系统与曲面的基本形式研究中的交互式应用。首先，我们将介绍曲线的 Frenet 系统和曲面的基本形式的定义和计算方法，并通过 Mathematica 实现这些计算。随后，我们将利用 Mathematica 的交互式绘图功能，展示经典曲线和曲面的几何特征，包括曲线的切线、基本三棱形以及曲面的法线和切平面。通过这些研究，我们可以更直观地理解微分几何中各种重要的几何概念和性质。

# 二、曲线的 Frenet 系统与曲面的基本形式的研究

Mathematica 作为强大的数学工业软件，在代数计算与几何交互等领域的功能十分强大。在正式研究曲线和曲面的几何特征之前，我们首先使用 Mathematica 来进行代数计算，分别计算曲线的 Frenet 系统参数与曲面的基本形式。

## 2.1	曲线的 Frenet 系统的研究

在微分几何中，Frenet 系统描述的是质点沿三维 Euclid 空间$\mathbb{R}^3$中的可变曲线运动的运动学性质，或者说是曲线本身的几何性质。更具体地说，这些公式描述了所谓的单位切向量、主法向量和副法向量以及曲线的曲率和挠率。

### 2.1.1	曲线的 Frenet 系统的定义

单位切向量、主法向量和副法向量共同构成三维 Euclid 空间$\mathbb{R}^3$的单位正交基，对于曲线$\boldsymbol{r}=\boldsymbol{r}(s)$，其定义如下
$$
\boldsymbol{\alpha}=\dot{\boldsymbol{r}},\qquad 
\boldsymbol{\beta}=\frac{\dot{\boldsymbol{\alpha}}}{|\dot{\boldsymbol{\alpha}}|},\qquad 
\boldsymbol{\gamma}=\boldsymbol{\alpha}\times\boldsymbol{\beta}
$$

曲率刻画了曲线的弯曲程度，挠率刻画了曲线的扭转程度，对于曲线$\boldsymbol{r}=\boldsymbol{r}(s)$​，其定义如下
$$
k=|\dot{\boldsymbol{\alpha}}|,\qquad
\tau=\begin{cases}
|\dot{\boldsymbol{\gamma}}|,\qquad & \boldsymbol{\gamma}'\text{与}\boldsymbol{\beta}\text{异向}\\
-|\dot{\boldsymbol{\gamma}}|,\qquad & \boldsymbol{\gamma}'\text{与}\boldsymbol{\beta}\text{同向}
\end{cases}
$$

### 2.1.2	曲线的 Frenet 系统的计算

在 Mathematica 中可以使用 `FrenetSerretSystem` 函数计算出曲线的 Frenet 系统的参数，如下表格展示了圆柱螺线与双曲螺线的 Frenet 系统的参数。

|   曲线   |                        参数表示                        |                    $\boldsymbol{\alpha}$                     |   $\boldsymbol{\beta}$    |                    $\boldsymbol{\gamma}$                     |          $k$           |         $\tau$         |
| :------: | :----------------------------------------------------: | :----------------------------------------------------------: | :-----------------------: | :----------------------------------------------------------: | :--------------------: | :--------------------: |
| 圆柱螺线 | $\begin{cases}x=a\cos t\\y=a\sin t\\ z=bt\end{cases}$  | $\left(-\frac{\sin (t)}{\sqrt{2}},\frac{\cos   (t)}{\sqrt{2}},\frac{1}{\sqrt{2}}\right)$ | $(-\cos (t),-\sin (t),0)$ | $\left(\frac{\sin (t)}{\sqrt{2}},-\frac{\cos(t)}{\sqrt{2}},\frac{1}{\sqrt{2}}\right)$ |     $\frac{1}{2}$      |     $\frac{1}{2}$      |
| 双曲螺线 | $\begin{cases}x=a\cosh t\\y=a\sinh t\\z=at\end{cases}$ | $\left(\frac{\sinh (t)}{\sqrt{\cosh (2 t)+1}},\frac{\cosh (t)}{\sqrt{\cosh (2   t)+1}},\frac{1}{\sqrt{\sinh ^2(t)+\cosh ^2(t)+1}}\right)$ | $(\sech(t),0,-\tanh(t))$  | $\left(-\frac{\sinh (t)}{\sqrt{\cosh (2 t)+1}},\frac{\cosh (t)}{\sqrt{\cosh (2   t)+1}},-\frac{1}{\sqrt{\cosh (2 t)+1}}\right)$ | $\frac{\sech^2(t)}{2}$ | $\frac{\sech^2(t)}{2}$ |

## 2.2	曲面的基本形式的研究

在微分几何中，曲面的第一基本形式刻画了曲面的度量特性，如长度和面积等，通常以Ⅰ表示；曲面的第二基本形式与第一基本形式一起刻画了曲面的外在不变式，如曲面上曲线的曲率，通常以Ⅱ表示。

### 2.2.1	曲面的基本形式的定义

对于曲面$ \boldsymbol{r}=\boldsymbol{r}(u,v)$，其第一基本形式定义如下
$$
Ⅰ=
{\mathrm{d}}\boldsymbol{r}^2
=E\mathrm{d} u^2+2F\mathrm{d} u\mathrm{d} v+G\mathrm{d} v^2=
\begin{pmatrix}
\mathrm{d} u & \mathrm{d} v
\end{pmatrix}
\begin{pmatrix}
E & F\\
F & G
\end{pmatrix}
\begin{pmatrix}
\mathrm{d} u \\ \mathrm{d} v
\end{pmatrix}
$$

其中
$$
E=\boldsymbol{r}_u\cdot\boldsymbol{r}_u,\qquad 
	F=\boldsymbol{r}_u\cdot\boldsymbol{r}_v,\qquad 
	G=\boldsymbol{r}_v\cdot\boldsymbol{r}_v
$$
称为曲面的第一类基本量。

对于曲面$ \boldsymbol{r}=\boldsymbol{r}(u,v)$​，其第二基本形式定义如下
$$
Ⅱ=\boldsymbol{n}\cdot{\mathrm{d}}^2\boldsymbol{r}
=L\mathrm{d} u^2+2M\mathrm{d} u\mathrm{d} v+N\mathrm{d} v^2=
\begin{pmatrix}
\mathrm{d} u & \mathrm{d} v
\end{pmatrix}
\begin{pmatrix}
L & M\\
M & N
\end{pmatrix}
\begin{pmatrix}
\mathrm{d} u \\ \mathrm{d} v
\end{pmatrix}
$$
其中
$$
L=\boldsymbol{r}_{uu}\cdot\boldsymbol{n}=\frac{(\boldsymbol{r}_{uu},\boldsymbol{r}_u,\boldsymbol{r}_v)}{\sqrt{EG-F^2}},\quad 
	M=\boldsymbol{r}_{uv}\cdot\boldsymbol{n}=\frac{(\boldsymbol{r}_{uv},\boldsymbol{r}_u,\boldsymbol{r}_v)}{\sqrt{EG-F^2}},\quad 
	N=\boldsymbol{r}_{vv}\cdot\boldsymbol{n}=\frac{(\boldsymbol{r}_{vv},\boldsymbol{r}_u,\boldsymbol{r}_v)}{\sqrt{EG-F^2}}
$$
称为曲面的第二类基本量。

### 2.2.2	曲面的基本形式的计算

在 Mathematica 中没有直接计算曲面的基本形式的函数，因此我们需要自行定义。如下表格展示了球面与正螺面的基本形式。

|  曲面  |                           参数表示                           |                    第一基本形式                    |                 第二基本形式                  |
| :----: | :----------------------------------------------------------: | :------------------------------------------------: | :-------------------------------------------: |
|  球面  | $\begin{cases}x=\rho\cos\theta\cos\varphi\\y=\rho\cos\theta\sin\varphi\\z=\rho\sin\theta\end{cases}$ | $\rho^2\cos^2\theta\mathrm{d}\varphi^2+\rho^2\mathrm{d}\theta^2$ | $-\rho(\cos^2\theta\mathrm{d}\varphi^2+\mathrm{d}\theta^2)$ |
| 正螺面 |     $\begin{cases}x=u\cos v\\y=u\sin v\\z=av\end{cases}$     |             $\mathrm{d} u^2+(u^2+a^2)\mathrm{d} v^2$             |    $-\frac{2a}{\sqrt{u^2+a^2}}\mathrm{d} u\mathrm{d} v$     |

# 三、经典曲线与曲面研究的交互式应用

## 3.1 圆柱螺线与马鞍面的交互式绘图

为后文便于研究曲线和曲面的几何特征，我们在这里首先给出圆柱螺线与马鞍面的定义，并作交互式绘图。

### 3.1.1	圆柱螺线与马鞍面的定义

圆柱螺线定义如下
$$
\begin{cases}
x=a\cos t\\
y=a\sin t\\
z=bt
\end{cases}\qquad 
t\text{为参数}
$$
马鞍面定义如下
$$
\frac{x^2}{a^2}-\frac{y^2}{b^2}=z
$$

### 3.1.2	圆柱螺线与马鞍面的交互式绘图

圆柱螺线的交互式绘图如下

其中我们可以自由调整参数$a,b,t$的大小。通过调整参数$a,b,t$的大小，我们可以发现若把圆柱螺线当作弹簧，则参数$a$越大，该弹簧被拉伸程度越小；参数$b$越大，该弹簧长度越长；参数$t$越大，该弹簧弯曲圈数越多。

马鞍面的交互式绘图如下

其中我们可以自由调整参数$a,b$的大小。通过调整参数$a,b$的大小，我们可以发现参数$a$越大，$y$轴方向上的凹陷程度越小；参数$b$越大，$x$轴方向下的凹陷程度越小。

## 3.2 旋轮线的交互式绘图

在正式开始研究曲线与曲面的几何特征之前，我们稍作放松，来研究一个有意思的平面曲线。

### 3.2.1	旋轮线的定义

考虑数轴
$$
0\le x\le 6\pi
$$
上切于$(0,0)$的单位圆
$$
x=\cos t,\qquad 
y=1+\sin t
$$
令$\varphi$表示圆周转过的角度，那么此时圆周的方程为
$$
x=\varphi+\cos t,\qquad 
y=1+\sin t
$$
记初始圆上切于数轴的点为$P$，当圆周沿数轴运动时，点$P$也在运动，其参数表示为
$$
P(\varphi-\sin\varphi,1-\cos\varphi)
$$
那么$P$的轨迹方程为
$$
\begin{cases}
x=\varphi-\sin\varphi\\
y=1-\cos\varphi
\end{cases},\qquad \varphi\text{ 为参数}
$$
称之为旋轮线。

### 3.2.2	旋轮线的交互式绘图

我们使用 Mathematica 将上述运动过程展示出来，通过调整圆周转过的角度$\varphi$，来显示圆周与点$P$不同的位置，以及点$P$​​的运动轨迹，如下图所示。

# 四、曲线的几何特征研究的交互式应用

## 4.1	曲线的切线的交互式绘图

在几何学中，曲线在某一点的切线，直观地说就是在该点 “刚好接触 ”曲线的直线。更精确地说，如果一条直线经过曲线上的点$(x_0,f(x_0))$，并且具有斜率$f'(x_0)$，那么这条直线就是曲线$y = f(x)$在点$x=x_0$处的切线。切线与曲线的交点称为切点。切线与曲线 “同向”，因此是曲线在该点的最佳直线近似值。

### 4.1.1	曲线的切线的定义

对于曲线$\boldsymbol{r}=\boldsymbol{r}(t)$，其切线的参数方程为
$$
\boldsymbol{p}-\boldsymbol{r}(t)=\lambda\boldsymbol{r}'(t),\qquad 
\lambda\text{为参数}
$$
若写为直线的标准形式，则为
$$
\frac{X-x(t)}{x'(t)}
=\frac{Y-y(t)}{y'(t)}
=\frac{Z-z(t)}{z'(t)}
$$

### 4.1.2	圆柱螺线的切线的交互式绘图

下面我们给出圆柱螺线在不同点处的切线的交互式绘图。

## 4.2	曲线的基本三棱形的交互式绘图

### 4.2.1	曲线的基本三棱形的定义

从上文中我们知道单位切向量、主法向量和副法向量共同构成三维Euclid空间$\mathbb{R}^3$的单位正交基，为此我们可以定义曲线的三个有关平面。

密切平面为以$\boldsymbol{\gamma}$为法向的平面，方程为
$$
(\boldsymbol{p}-\boldsymbol{r},\boldsymbol{r}',\boldsymbol{r}'')=0
$$
法平面为以$\boldsymbol{\alpha}$为法向的平面，方程为
$$
(\boldsymbol{p}-\boldsymbol{r})\cdot \boldsymbol{r}'=0
$$
从切平面为以$\boldsymbol{\beta}$为法向的平面，方程为
$$
(\boldsymbol{p}-\boldsymbol{r})\cdot \boldsymbol{r}''=0
$$

这三个特征平面两两垂直，共同构成曲线的基本三棱形。

### 4.2.2	圆柱螺线的基本三棱形的交互式绘图

下面我们给出圆柱螺线在不同点处的基本三棱形的交互式绘图。

# 五、曲面的几何特征研究的交互式应用

## 5.1	曲面的法线的交互式绘图

### 5.1.1	曲面的法线的定义

在几何中，法向量是垂直于给定对象的向量。在三维 Eucilid 空间$\mathbb{R}^3$中，通过曲面上给定一点且以该点的法向量为方向的直线称之为曲面在该点处的法线。

对于曲面$\boldsymbol{r}=\boldsymbol{r}(u,v)$​，其法向量为
$$
\boldsymbol{n}=\frac{\boldsymbol{r}_u\times\boldsymbol{r}_v}{|\boldsymbol{r}_u\times\boldsymbol{r}_v|}
$$
法线的参数方程为
$$
\boldsymbol{p}-\boldsymbol{r}=\lambda\boldsymbol{r}_u\times\boldsymbol{r}_v,\qquad
\lambda\text{为参数}
$$
若写为直线的标准形式，则为
$$
\frac{X-x(u,v)}{\begin{vmatrix}
y_u(u,v) & z_u(u,v)\\
y_v(u,v) & z_v(u,v)
\end{vmatrix}}
=\frac{Y-y(u,v)}{\begin{vmatrix}
z_u(u,v) & x_u(u,v)\\
z_v(u,v) & x_v(u,v)
\end{vmatrix}}
=\frac{Z-z(u,v)}{\begin{vmatrix}
x_u(u,v) & y_u(u,v)\\
x_v(u,v) & y_v(u,v)
\end{vmatrix}}
$$

### 5.1.2	马鞍面的法线的交互式绘图

下面我们给出马鞍面在不同点处的法线的交互式绘图。

## 5.2	曲面的切平面的交互式绘图

### 5.2.1	曲面的切平面的定义

在三维 Eucilid 空间$\mathbb{R}^3$​中，通过曲面上给定一点且以垂直于该点的法向量的平面称之为曲面在该点处的切平面。

对于曲面$\boldsymbol{r}=\boldsymbol{r}(u,v)$​，切平面的参数方程为
$$
(\boldsymbol{p}-\boldsymbol{r},\boldsymbol{r}_u,\boldsymbol{r}_v)=0
$$
若写为平面的标准形式，则为
$$
\begin{vmatrix}
X-x(u,v) & Y-y(u,v) & Z-z(u,v)\\
x_u(u,v) & y_u(u,v) & z_u(u,v)\\
x_v(u,v) & y_v(u,v) & z_v(u,v)
\end{vmatrix}=0
$$

### 5.2.2	马鞍面的切平面的交互式绘图

下面我们给出马鞍面在不同点处的切平面的交互式绘图。

# 六、结语

本文通过具体实例展示了 Mathematica 在微分几何研究中的强大功能和应用价值。通过使用 Mathematica 进行曲线 Frenet 系统和曲面基本形式的研究，我们能够更方便地进行复杂的代数计算和几何图形的可视化。这不仅提高了研究效率，还使得一些抽象的几何概念变得更加直观易懂。

此外，本文还通过交互式绘图展示了经典曲线和曲面的几何特征，使得读者能够通过调整参数，直观地观察到几何图形的变化和特征。这种交互式的学习方式，不仅有助于加深对微分几何知识的理解，也为进一步的研究提供了便利。

总的来说，Mathematica 为微分几何的研究提供了强有力的工具，使得复杂的数学问题变得更易于处理和理解。希望本文的研究能够为从事微分几何研究的学者提供有益的参考，同时也期待更多数学软件在数学研究中的广泛应用。

# 附录

1.空间曲线的Frenet系统

```mathematica
(*圆柱螺线的 Frenet 系统参数*)
curve[t_] := {Cos[t], Sin[t], t}
{{\[Kappa], \[Tau]}, {\[Alpha], \[Beta], \[Gamma]}} = 
 FrenetSerretSystem[curve[t], t] // Simplify

(*双曲螺线的 Frenet 系统参数*)
curve[t_] := {Cosh[t], Sinh[t], t}
{{\[Kappa], \[Tau]}, {\[Alpha], \[Beta], \[Gamma]}} = 
 FrenetSerretSystem[curve[t], t] // Simplify
```

2.曲面的基本形式

```mathematica
(* 定义曲面的第一类基本量 *)
FirstFundamentalFormCoefficients[r_, {u_, v_}] := Module[{
      du = \!\(
\*SubscriptBox[\(\[PartialD]\), \(u\)]r\), 
      dv = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]r\)}, 
    {du . du, du . dv, dv . dv}]

(* 定义曲面的第二类基本量 *)
SecondFundamentalFormCoefficients[r_List, {u_, v_}] := Module[{
      du = \!\(
\*SubscriptBox[\(\[PartialD]\), \(u\)]r\), 
      dv = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]r\), 
      dudu, dudv, dvdv}, 
    dudu = \!\(
\*SubscriptBox[\(\[PartialD]\), \(u\)]du\); 
    dudv = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]du\); 
    dvdv = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]dv\); 
    {Det[{dudu, du, dv}], Det[{dudv, du, dv}], Det[{dvdv, du, dv}]}/
      Sqrt[du . du dv . dv - (du . dv)^2]]

(* 求解球面的基本形式 *)
r[\[CurlyPhi]_, \[Theta]_] := {\[Rho] Cos[\[Theta]] Cos[\[CurlyPhi]], \
\[Rho] Cos[\[Theta]] Sin[\[CurlyPhi]], \[Rho] Sin[\[Theta]]}
(* 计算球面的第一类基本量 *)
FirstFundamentalFormCoefficients[
    r[\[CurlyPhi], \[Theta]], {\[CurlyPhi], \[Theta]}] // Simplify
(* 计算球面的第二类基本量 *)
SecondFundamentalFormCoefficients[
    r[\[CurlyPhi], \[Theta]], {\[CurlyPhi], \[Theta]}] // Simplify

(* 求解正螺面的基本形式 *)
r[u_, v_] := {u Cos[v], u Sin[v], a v}
(* 计算正螺面的第一类基本量 *)
FirstFundamentalFormCoefficients[r[u, v], {u, v}] // Simplify
(* 计算正螺面的第二类基本量 *)
SecondFundamentalFormCoefficients[r[u, v], {u, v}] // Simplify
```

3.圆柱螺线

```mathematica
(* 使用Manipulate创建交互式绘图 *)
Manipulate[ParametricPlot3D[
  {a Cos[t], a Sin[t], b t}, {t, 0.0, tMax},
  PlotStyle -> Red,
  PlotRange -> All,
  AxesLabel -> {"x", "y", "z"},
  ViewPoint -> {2.4, 1.3, 2},
  ImageSize -> Scaled[0.25]],
 {{a, 0}, -1, 1}, {{b, 0}, -1, 1}, {{tMax, 5}, 0, 4 Pi}]
```

4.马鞍面

```mathematica
(* 使用Manipulate创建交互式绘图 *)
Manipulate[ParametricPlot3D[
  {x, y, x^2/a^2 - y^2/b^2}, {x, -max, max}, {y, -max, max},
  PlotStyle -> Orange,
  PlotRange -> All,
  AxesLabel -> {"x", "y", "z"},
  ViewPoint -> {2.4, 1.3, 2},
  ImageSize -> Scaled[0.25]],
 {{a, 3}, 1, 5}, {{b, 3}, 1, 5}, {{max, 5}, 0, 10}]
```

5.旋轮线

```mathematica
(*使用Manipulate创建交互式绘图*)
Manipulate[
 Module[
  {line, circle, point, pointCurve},
  (* 绘制数轴 *)
  line = Graphics[{
     Line[{{0, 0}, {6 Pi, 0}}],
     Table[Text[ToString[i] <> "\[Pi]", {i Pi, -0.5}], {i, 0, 6}]}];
  (* 绘制圆周 *)
  circle = ParametricPlot[
    {Cos[t] + \[CurlyPhi], 1 + Sin[t]}, {t, 0, 2 Pi},
    PlotStyle -> Blue];
  (* 绘制动点 *)
  point = Graphics[{
     PointSize[Large],
     Red,
     Point[{\[CurlyPhi] - Sin[\[CurlyPhi]], 1 - Cos[\[CurlyPhi]]}]}];
  (* 绘制动点轨迹 *)
  pointCurve = ParametricPlot[
    {t - Sin[t], 1 - Cos[t]}, {t, 0, \[CurlyPhi]},
    PlotStyle -> Red
    ];
  Show[line, circle, point, pointCurve, 
   PlotRange -> {{-2, 6 Pi + 2}, {-2, 3}}, ImageSize -> Scaled[0.75], 
   Epilog -> {Text[
      Style["\[CurlyPhi] = " <> ToString[\[CurlyPhi]/\[Pi]] <> 
        "\[Pi]", 16, Bold], Scaled[{0.85, 0.9}]]}]],
 {\[CurlyPhi], 0.001, 6 Pi}]


GraphicsColumn[Table[Module[{line, circle, point, pointCurve},
   (*设置 n 的值*)
   n = nValue;
   (*计算 phi*)
   \[CurlyPhi] = n \[Pi];
   (*绘制数轴*)
   line = 
    Graphics[{Line[{{0, 0}, {6 Pi, 0}}], 
      Table[Text[ToString[i] <> "\[Pi]", {i Pi, -0.5}], {i, 0, 6}]}];
   (*绘制圆周*)
   circle = 
    ParametricPlot[{Cos[t] + \[CurlyPhi], 1 + Sin[t]}, {t, 0, 2 Pi}, 
     PlotStyle -> Blue];
   (*绘制动点*)
   point = 
    Graphics[{PointSize[Large], Red, 
      Point[{\[CurlyPhi] - Sin[\[CurlyPhi]], 1 - Cos[\[CurlyPhi]]}]}];
   (*绘制动点轨迹*)
   pointCurve = 
    ParametricPlot[{t - Sin[t], 1 - Cos[t]}, {t, 0, \[CurlyPhi]}, 
     PlotStyle -> Red];
   Show[line, circle, point, pointCurve, 
    PlotRange -> {{-2, 6 Pi + 2}, {-2, 3}}, ImageSize -> Scaled[1.5], 
    Epilog -> {Text[
       Style["\[CurlyPhi] = " <> ToString[\[CurlyPhi]/\[Pi]] <> 
         "\[Pi]", 16, Bold], Scaled[{0.85, 0.9}]]}]],
  {nValue, {2, 3.25, 5.5}}]]
```

6.空间曲线的切线

```mathematica
(* 定义参数化空间曲线 *)
r[t_] := {Cos[t], Sin[t], t}

(* 计算曲线的导数 *)
dr[t_] = D[r[t], t];

(* Subscript[定义t, 0]处切线的参数方程 *)
tangentLine[t0_, s_] := r[t0] + s dr[t0]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[
  {curve, tangent, pt},
  (* 绘制空间曲线 *)
  curve = ParametricPlot3D[
    r[t], {t, 0, 2 Pi},
    PlotStyle -> Blue,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* Subscript[绘制点t, 0]处的切线 *)
  tangent = ParametricPlot3D[
    tangentLine[t0, s], {s, -1, 1},
    PlotStyle -> {Red, Thick},
    PlotRange -> All];
  (* Subscript[绘制t, 0]点 *)
  pt = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[t0]]}];
  (* 组合所有图形 *)
  Show[curve, tangent, pt,
   ViewPoint -> {2.4, 1.3, 2},
   ImageSize -> Scaled[0.25],
   Epilog -> {Text[
      Style["\!\(\*SubscriptBox[\(t\), \(0\)]\) = " <> 
        ToString[Round[t0, 0.01]], 16, Bold], Scaled[{0.8, 0.9}]]}]],
 {t0, 0, 2 Pi}]
```

7.空间曲线的基本三棱形

```mathematica
(* 定义参数化空间曲线 *)
r[t_] := {Cos[t], Sin[t], t}

(* 计算曲线的一阶导数 *)
dr[t_] = D[r[t], t];

(* 计算曲线的二阶导数 *)
dr2[t_] = D[dr[t], t];

(* Subscript[定义t, 0]处密切平面的参数方程 *)
closePlane[t0_, X_] := Dot[X - r[t0], Cross[dr[t0], dr2[t0]]]

(* Subscript[定义t, 0]处法平面的参数方程 *)
normalPlane[t0_, X_] := Dot[X - r[t0], dr[t0]]
(* Subscript[定义t, 0]处从切平面的参数方程 *)
rectifyingPlane[t0_, X_] := Dot[X - r[t0], dr2[t0]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[{curve, pt, closeplane, normalplane, rectifyingplane},
  (* 绘制空间曲线 *)
  curve = ParametricPlot3D[
    r[t], {t, 0, 2 \[Pi]},
    PlotStyle -> Red,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* Subscript[绘制t, 0]点 *)
  pt = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[t0]]}];
  (* Subscript[绘制点t, 0]处的密切平面 *)
  closeplane = ContourPlot3D[
    closePlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Blue],
    Mesh -> None];
  (* Subscript[绘制点t, 0]处的法平面 *)
  normalplane = ContourPlot3D[
    normalPlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Orange],
    Mesh -> None];
  (* Subscript[绘制点t, 0]处的从切平面 *)
  rectifyingplane = ContourPlot3D[
    rectifyingPlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Green],
    Mesh -> None];
  (* 组合所有图形 *)
  Show[curve, pt, closeplane, normalplane, rectifyingplane,
   ViewPoint -> {2.4, 1.3, 2}, ImageSize -> Scaled[0.5],
   Epilog -> {Text[
      Style["\!\(\*SubscriptBox[\(t\), \(0\)]\) = " <> 
        ToString[Round[t0, 0.01]], 16, Bold], 
      Scaled[{0.85, 0.9}]]}]], {t0, 0, 2 \[Pi]}]
```

8.曲面的法线

```mathematica
(* 定义参数化曲面 *)
r[u_, v_] := {u, v, u^2 - v^2}

(* 计算曲线的偏导数 *)
du[u_, v_] = \!\(
\*SubscriptBox[\(\[PartialD]\), \(u\)]\(r[u, v]\)\);
dv[u_, v_] = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]\(r[u, v]\)\);


(* 定义(u0,v0)法线的参数方程 *)
normalLine[u0_, v0_, s_] := r[u0, v0] + s Cross[du[u0, v0], dv[u0, v0]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[
  {surface, normal, point},
  (* 绘制曲面 *)
  surface = ParametricPlot3D[
    r[u, v], {u, -2, 2}, {v, -2, 2},
    PlotStyle -> Orange,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制点(u0,v0)处的法线 *)
  normal = ParametricPlot3D[
    normalLine[u0, v0, s], {s, -1, 1},
    PlotStyle -> Red,
    PlotRange -> All];
  (* 绘制(u0,v0)点 *)
  point = Graphics3D[
    {Red,
     PointSize[Large],
     Point[r[u0, v0]]}];
  (* 组合所有图形 *)
  Show[surface, normal, point,
   ViewPoint -> {2.4, 1.3, 2},
   ImageSize -> Scaled[0.5],
   Epilog -> {
     Text[
      Style["\!\(\*SubscriptBox[\(u\), \(0\)]\) = " <> ToString[u0], 
       16, Bold], Scaled[{0.8, 0.9}]],
     Text[
      Style["\!\(\*SubscriptBox[\(v\), \(0\)]\) = " <> ToString[v0], 
       16, Bold], Scaled[{0.8, 0.85}]]
     }]],
 {u0, -2, 2}, {v0, -2, 2}]
```

9.曲面的切平面

```mathematica
(*定义参数化曲面*)
r[u_, v_] := {u, v, u^2 - v^2}

(*计算曲线的偏导数*)
du[u_, v_] = \!\(
\*SubscriptBox[\(\[PartialD]\), \(u\)]\(r[u, v]\)\);
dv[u_, v_] = \!\(
\*SubscriptBox[\(\[PartialD]\), \(v\)]\(r[u, v]\)\);

(* Subscript[定义t, 0]处切平面的参数方程 *)
tangentPlane[u0_, v0_, X_] := 
 Dot[X - r[u0, v0], Cross[du[u0, v0], dv[u0, v0]]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[{surface, point, normalVector, plane},
  (* 绘制曲面 *)
  surface = ParametricPlot3D[
    r[u, v], {u, -2, 2}, {v, -2, 2},
    PlotStyle -> Orange,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* Subscript[绘制t, 0]点 *)
  point = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[u0, v0]]}];
  (* Subscript[绘制点t, 0]处的法向量 *)
  normalVector = Graphics3D[{
     Red,
     Arrow[{
       r[u0, v0], r[u0, v0] + Cross[du[u0, v0], dv[u0, v0]]
       }]}];
  (* Subscript[绘制点t, 0]处的切平面 *)
  plane = ContourPlot3D[
    tangentPlane[u0, v0, {X, Y, Z}] == 0,
    {X, r[u0, v0][[1]] - 2, r[u0, v0][[1]] + 2},
    {Y, r[u0, v0][[2]] - 2, r[u0, v0][[2]] + 2},
    {Z, r[u0, v0][[3]] - 2, r[u0, v0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Yellow],
    Mesh -> None];
  (* 组合所有图形 *)
  Show[surface, point, normalVector, plane,
   ViewPoint -> {2.4, 1.3, 2},
   ImageSize -> Scaled[0.5],
   Epilog -> {
     Text[
      Style["\!\(\*SubscriptBox[\(u\), \(0\)]\) = " <> ToString[u0], 
       16, Bold], Scaled[{0.8, 0.95}]],
     Text[
      Style["\!\(\*SubscriptBox[\(v\), \(0\)]\) = " <> ToString[v0], 
       16, Bold], Scaled[{0.8, 0.9}]]}]],
 {u0, -2, 2}, {v0, -2, 2}]
```

