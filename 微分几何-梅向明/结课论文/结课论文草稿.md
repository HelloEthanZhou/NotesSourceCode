# 第一章：曲线论

## 1.1	曲线

**圆柱螺线**：
$$
\begin{cases}
x=a\cos t\\
y=a\sin t\\
z=bt
\end{cases}\qquad 
t\in\R
$$

```mathematica
(* 使用Manipulate创建交互式绘图 *)
Manipulate[ParametricPlot3D[
  {a Cos[t], a Sin[t], b t}, {t, 0.01, tMax},
  PlotStyle -> Red,
  PlotRange -> All,
  AxesLabel -> {"x", "y", "z"}],
 {{a, 0}, -1, 1}, {{b, 0}, -1, 1}, {{tMax, 5}, 0, 10}]
```

**曲线的切线**：
$$
\boldsymbol{p}-\boldsymbol{r}(t)=\lambda\boldsymbol{r}'(t),\qquad 
\lambda\text{为参数}
$$
```mathematica
(* 定义参数化空间曲线 *)
r[t_] := {Cos[t], Sin[t], t}

(* 计算曲线的导数 *)
dr[t_] = D[r[t], t];

(* 定义t0处切线的参数方程 *)
tangentLine[t0_, s_] := r[t0] + s dr[t0]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[
  {curve, tangent, pt},
  (* 绘制空间曲线 *)
  curve = ParametricPlot3D[
    r[t], {t, 0, 2 Pi},
    PlotStyle -> Blue,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制点t0处的切线 *)
  tangent = ParametricPlot3D[
    tangentLine[t0, s], {s, -1, 1},
    PlotStyle -> {Red, Thick},
    PlotRange -> All];
  (* 绘制t0点 *)
  pt = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[t0]]}];
  (* 组合所有图形 *)
  Show[curve, tangent, pt]],
 {t0, 0, 2 Pi}]
```

**曲线的法平面**：
$$
(\boldsymbol{p}-\boldsymbol{r}(t))\cdot\boldsymbol{r}'(t)=0
$$

```mathematica
(* 定义参数化空间曲线 *)
r[t_] := {Cos[t], Sin[t], t}

(* 计算曲线的导数 *)
dr[t_] = D[r[t], t];

(* 定义t0处法平面的参数方程 *)
normalPlane[t0_, X_] := Dot[X - r[t0], dr[t0]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[{curve, pt, normalVector, plane},
  (* 绘制空间曲线 *)
  curve = ParametricPlot3D[
    r[t], {t, 0, 2 Pi},
    PlotStyle -> Blue,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制t0点 *)
  pt = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[t0]]}];
  (* 绘制点t0处的法向量 *)
  normalVector = Graphics3D[{
     Red,
     Arrow[{
       r[t0], r[t0] + dr[t0]
       }]}];
  (* 绘制点t0处的法平面 *)
  plane = ContourPlot3D[
    normalPlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Orange],
    Mesh -> None];
  (* 组合所有图形 *)
  Show[curve, pt, normalVector, plane]],
 {t0, 0, 2 Pi}]
```

## 1.2	空间曲线

**空间曲线的密切平面**：
$$
(\boldsymbol{p}-\boldsymbol{r},\boldsymbol{r}',\boldsymbol{r}'')=0
$$

**空间曲线的法平面**：
$$
(\boldsymbol{p}-\boldsymbol{r})\cdot \boldsymbol{r}'=0
$$

**空间曲线的从切平面**：
$$
(\boldsymbol{p}-\boldsymbol{r})\cdot \boldsymbol{r}''=0
$$

```mathematica
(* 定义参数化空间曲线 *)
r[t_] := {Cos[t], Sin[t], t}

(* 计算曲线的一阶导数 *)
dr[t_] = D[r[t], t];

(* 计算曲线的二阶导数 *)
dr2[t_] = D[dr[t], t];

(* 定义t0处密切平面的参数方程 *)
closePlane[t0_, X_] := Dot[X - r[t0], Cross[dr[t0], dr2[t0]]]

(* 定义t0处法平面的参数方程 *)
normalPlane[t0_, X_] := Dot[X - r[t0], dr[t0]]
(* 定义t0处从切平面的参数方程 *)
rectifyingPlane[t0_, X_] := Dot[X - r[t0], dr2[t0]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[{curve, pt, closeplane, normalplane, rectifyingplane},
  (* 绘制空间曲线 *)
  curve = ParametricPlot3D[
    r[t], {t, 0, 2 Pi},
    PlotStyle -> Blue,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制t0点 *)
  pt = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[t0]]}];
  (* 绘制点t0处的密切平面 *)
  closeplane = ContourPlot3D[
    closePlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Blue],
    Mesh -> None];
  (* 绘制点t0处的法平面 *)
  normalplane = ContourPlot3D[
    normalPlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Orange],
    Mesh -> None];
  (* 绘制点t0处的从切平面 *)
  rectifyingplane = ContourPlot3D[
    rectifyingPlane[t0, {X, Y, Z}] == 0,
    {X, r[t0][[1]] - 2, r[t0][[1]] + 2},
    {Y, r[t0][[2]] - 2, r[t0][[2]] + 2},
    {Z, r[t0][[3]] - 2, r[t0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Green],
    Mesh -> None];
  (* 组合所有图形 *)
  Show[curve, pt, closeplane, normalplane, rectifyingplane]],
 {t0, 0, 2 Pi}]
```

**空间曲线的曲率，挠率与Frenet公式**：

```mathematica
(* 使用 Mathematica 求解 Frenet 系统参数 *)
x[t_] := Cos[t]
y[t_] := Sin[t]
z[t_] := t
curve[t_] := {x[t], y[t], z[t]}
{{kappa, tau}, {alpha, beta, gamma}} = FrenetSerretSystem[curve[t], t] // Simplify
```

## 1.3	平面上的特殊曲线

考虑数轴
$$
0\le x\le 20
$$
上切于$(0,0)$的圆
$$
x=\cos t,\qquad 
y=1+\sin t
$$
令$\varphi$表示圆周转过的角度，那么此时圆周的方程为
$$
x=\varphi+\cos t,\qquad 
y=1+\sin t
$$
考察初始圆上切于数轴的点$P$的参数表示
$$
P(\varphi-\sin\varphi,1-\cos\varphi)
$$
那么$P$的轨迹方程
$$
\begin{cases}
x=\varphi-\sin\varphi\\
y=1-\cos\varphi
\end{cases},\qquad \varphi\text{ 为参数}
$$
称为旋轮线。

```mathematica
(*使用Manipulate创建交互式绘图*)
Manipulate[
 Module[
  {line, circle, point, pointCurve},
  (* 绘制数轴 *)
  line = Graphics[{
     Line[{{0, 0}, {6 Pi, 0}}],
     Table[Text[ToString[i] <> "\[Pi]", {i Pi, -0.5}], {i, 0, 6}]}];
  (* 绘制圆周 *)
  circle = ParametricPlot[
    {Cos[t] + \[CurlyPhi], 1 + Sin[t]}, {t, 0, 2 Pi},
    PlotStyle -> Blue];
  (* 绘制动点 *)
  point = Graphics[{
     PointSize[Large],
     Red,
     Point[{\[CurlyPhi] - Sin[\[CurlyPhi]], 1 - Cos[\[CurlyPhi]]}]}];
  (* 绘制动点轨迹 *)
  pointCurve = ParametricPlot[
    {t - Sin[t], 1 - Cos[t]}, {t, 0, \[CurlyPhi]},
    PlotStyle -> Red
    ];
  Show[line, circle, point, pointCurve, 
   PlotRange -> {{-2, 6 Pi + 2}, {-2, 3}}]],
 {\[CurlyPhi], 0.001, 6 Pi}]
```

# 第二章：曲面论

**马鞍面**：
$$
\frac{x^2}{a^2}-\frac{y^2}{b^2}=z
$$

```mathematica
(* 使用Manipulate创建交互式绘图 *)
Manipulate[ParametricPlot3D[
  {x, y, x^2/a^2 - y^2/b^2}, {x, -max, max}, {y, -max, max},
  PlotStyle -> Orange,
  PlotRange -> All,
  AxesLabel -> {"x", "y", "z"}],
 {{a, 3}, 1, 5}, {{b, 3}, 1, 5}, {{max, 5}, 0, 10}]
```

**曲面的切平面**：
$$
(\boldsymbol{p}-\boldsymbol{r},\boldsymbol{r}_u,\boldsymbol{r}_v)=0
$$
```mathematica
(*定义参数化曲面*)
r[u_, v_] := {u, v, u^2 - v^2}

(*计算曲线的偏导数*)
du[u_, v_] = D[r[u, v], u];
dv[u_, v_] = D[r[u, v], v];

(* 定义t0处切平面的参数方程 *)
tangentPlane[u0_, v0_, X_] := 
 Dot[X - r[u0, v0], Cross[du[u0, v0], dv[u0, v0]]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[{surface, point, normalVector, plane},
  (* 绘制曲面 *)
  surface = ParametricPlot3D[
    r[u, v], {u, -2, 2}, {v, -2, 2},
    PlotStyle -> Orange,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制t0点 *)
  point = Graphics3D[
    {Green,
     PointSize[Large],
     Point[r[u0, v0]]}];
  (* 绘制点t0处的法向量 *)
  normalVector = Graphics3D[{
     Red,
     Arrow[{
       r[u0, v0], r[u0, v0] + Cross[du[u0, v0], dv[u0, v0]]
       }]}];
  (* 绘制点t0处的切平面 *)
  plane = ContourPlot3D[
    tangentPlane[u0, v0, {X, Y, Z}] == 0,
    {X, r[u0, v0][[1]] - 2, r[u0, v0][[1]] + 2},
    {Y, r[u0, v0][[2]] - 2, r[u0, v0][[2]] + 2},
    {Z, r[u0, v0][[3]] - 2, r[u0, v0][[3]] + 2},
    ContourStyle -> Opacity[0.5, Yellow],
    Mesh -> None];
  (* 组合所有图形 *)
  Show[surface, point, normalVector, plane]],
 {u0, -2, 2}, {v0, -2, 2}]
```

**曲面的法线**：
$$
\boldsymbol{p}-\boldsymbol{r}=\lambda\boldsymbol{r}_u\times\boldsymbol{r}_v,\qquad
\lambda\text{为参数}
$$
```mathematica
(* 定义参数化曲面 *)
r[u_, v_] := {u, v, u^2 - v^2}

(* 计算曲线的偏导数 *)
du[u_, v_] = D[r[u, v], u];
dv[u_, v_] = D[r[u, v], v];

(* 定义(u0,v0)法线的参数方程 *)
normalLine[u0_, v0_, s_] := r[u0, v0] + s Cross[du[u0, v0], dv[u0, v0]]

(* 使用Manipulate创建交互式绘图 *)
Manipulate[
 Module[
  {surface, normal, point},
  (* 绘制曲面 *)
  surface = ParametricPlot3D[
    r[u, v], {u, -2, 2}, {v, -2, 2},
    PlotStyle -> Orange,
    PlotRange -> All,
    AxesLabel -> {"x", "y", "z"}];
  (* 绘制点(u0,v0)处的法线 *)
  normal = ParametricPlot3D[
    normalLine[u0, v0, s], {s, -1, 1},
    PlotStyle -> Green,
    PlotRange -> All];
  (* 绘制(u0,v0)点 *)
  point = Graphics3D[
    {Red,
     PointSize[Large],
     Point[r[u0, v0]]}];
  (* 组合所有图形 *)
  Show[surface, normal, point]],
 {u0, -2, 2}, {v0, -2, 2}]
```

**曲面的第一基本形式**：

```mathematica
(* 使用 Mathematica 求解曲面的第一类基本量 *)
(* 定义曲面的第一类基本量 *)
FirstFundamentalFormCoefficients[r_, {u_, v_}] := Module[{
du = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(u\)]r\), 
dv = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(v\)]r\)}, 
{du . du, du . dv, dv . dv}]
(* 定义曲面 *)
r[u_, v_] := {u Cos[v], u Sin[v], a*v}
(* 计算曲面的第一类基本量 *)
FirstFundamentalFormCoefficients[r[u, v], {u, v}] // Simplify
```

**曲面的第二基本形式**：

```mathematica
(* 使用 Mathematica 求解曲面的第二类基本量 *)
(* 定义曲面的第二类基本量 *)
SecondFundamentalFormCoefficients[r_List, {u_, v_}] := Module[{
du = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(u\)]r\), 
dv = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(v\)]r\), 
dudu, dudv, dvdv}, 
dudu = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(u\)]du\); 
dudv = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(v\)]du\); 
dvdv = \!\(\*SuboldsymbolcriptBox[\(\[PartialD]\), \(v\)]dv\); 
{Det[{dudu, du, dv}], Det[{dudv, du, dv}], Det[{dvdv, du, dv}]}/Sqrt[du . du dv . dv - (du . dv)^2]]
(* 定义曲面 *)
r[u_, v_] := {u, v, a (u^2 + v^2)}
(* 计算曲面的第二类基本量 *)
SecondFundamentalFormCoefficients[r[u, v], {u, v}] // Simplify
```
